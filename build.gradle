plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.4.20'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'maven'
    id 'signing'
}

group 'com.zp4rker'
archivesBaseName = project.name
version '1.4.20.1-release'

repositories {
    mavenCentral()
    maven { url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
    maven { url = "https://repo.codemc.org/repository/maven-public" }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compile "org.bstats:bstats-bukkit:1.7"
    shadow "org.spigotmc:spigot-api:1.16.4-R0.1-SNAPSHOT"
}

compileKotlin {
    kotlinOptions.jvmTarget = 1.8
}

shadowJar {
    archiveClassifier.set("")

    relocate "org.bstats", "com.zp4rker.bukkot.bstats"
}

processResources {
    filesMatching("**plugin.yml") {
        expand("version": project.property("version"))
    }
}

build.dependsOn "shadowJar"
build.dependsOn "processResources"

task javadocJar(type: Jar) {
    archiveClassifier.set("javadoc")
    from javadoc
}

task sourcesJar(type: Jar) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment d -> signing.signPom(d) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name "Bukkot"
                artifactId "bukkot"
                packaging "jar"
                description "Kotlin meets Bukkit. Kotlin packaged into a plugin, as well as some added features using Kotlin."
                url "https://github.com/zp4rker/bukkot"

                scm {
                    connection "scm:git:git://github.com/zp4rker/bukkot.git"
                    url "https://github.com/zp4rker/bukkot"
                }

                licenses {
                    license {
                        name "The Apache License, Version 2.0"
                        url "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }

                developers {
                    developer {
                        id "zp4rker"
                        name "Zaeem Parker"
                        email "iam@zp4rker.com"
                    }
                }
            }
        }
    }
}